/*
 * Copyright 2018 Hippo Seven
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'kotlin-platform-js'
apply plugin: 'com.moowork.node'

dependencies {
    expectedBy project(":client:common")
    testCompile project(":api2:js")
    testCompile project(":plugin:js")
    testCompile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
}

compileKotlin2Js {
    kotlinOptions.outputFile = "${project.buildDir.path}/classes/kotlin/main/client.js"
    kotlinOptions.moduleKind = 'commonjs'
}

compileTestKotlin2Js {
    kotlinOptions.outputFile = "${project.buildDir.path}/classes/kotlin/test/client.js"
    kotlinOptions.moduleKind = 'commonjs'
}

[compileKotlin2Js, compileTestKotlin2Js]*.configure {
    kotlinOptions.moduleKind = "commonjs"
}

task populateNodeModules(type: Copy, dependsOn: compileKotlin2Js) {
    from compileKotlin2Js.destinationDir

    configurations.testCompile.each {
        from zipTree(it.absolutePath).matching { include '*.js' }
    }

    into "${buildDir}/node_modules"
}

node {
    download = true
    version = "$node_version"
    yarnVersion = "$yarn_version"
    workDir = file("${rootProject.projectDir}/.gradle/nodejs")
    yarnWorkDir = file("${rootProject.projectDir}/.gradle/yarn")
}

task installMocha(type: YarnTask) {
    args = ["add", "mocha@$mocha_version", "-D"]
}

task runMocha(type: NodeTask, dependsOn: [compileTestKotlin2Js, populateNodeModules, installMocha]) {
    script = file("node_modules/mocha/bin/mocha")
    args = [compileTestKotlin2Js.outputFile]
}

test.dependsOn runMocha
